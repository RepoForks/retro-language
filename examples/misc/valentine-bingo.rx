( Port of http://groups.google.com/group/comp.lang.forth/browse_frm/thread/f37aff28d9a3102e )

needs array'

[ "Be Mine"
  "Love"
  "It's Love"
  "All Mine"
  "Kiss Me"
  "You & Me"
  "Awesome"
  "My Baby"
  "Love Me"
  "All Star"
  "My Way"
  "Love You"
  "For You"
  "Cool"
  "I Hope"
  "Love Life"
  "Angel"
  "Sweet Talk"
  "True Love"
  "Be Good"
  "How Nice"
  "Let's Kiss"
  "#1 Fan"
  "Be True"
  "Love Her" ] ^array'fromQuote constant WORDS

WORDS ^array'length constant #WORDS

"Free Square" string FREE

create SELECTED
  #WORDS dup , FREE , allot

create CARD
  #WORDS dup , allot

: nth  ( na-$ )
  1+ + @ ;

: rnd  ( -n )
  random #WORDS mod ;

variables| array bucket |

: obtain  ( ab-$ )
  !bucket !array
  0 [ drop rnd @array nth dup @bucket ^array'stringIn? ] while ;

: fill  ( - )
  SELECTED 2 + #WORDS [ WORDS SELECTED obtain swap !+ ] times drop ;

: final
  CARD 1+ #WORDS [ SELECTED CARD obtain swap !+ ] times drop ;

: card
  fill final CARD 1+
  5 [ "<tr>" puts 5 [ @+ "<td>%s</td>" puts cr ] times "</td>" puts ] times cr ;

: head
  "\n<html>\n" puts
  "<body>\n" puts
  "<table border=1 height=500 width=500>\n" puts
  "<tr style='color: red'>\n" puts
  "<th width='20%'>B</th>\n" puts
  "<th width='20%'>I</th>\n" puts
  "<th width='20%'>N</th>\n" puts
  "<th width='20%'>G</th>\n" puts
  "<th width='20%'>O</th>\n" puts
  "</tr>\n" puts ;

: footer
  "</table>\n" puts
  "</body>\n" puts
  "</html>\n" puts ;

: bingo  head card footer bye ;
bingo

