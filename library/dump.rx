
needs regAB'
with regAB'

needs subcell'
with subcell'

chain: dump'

{{
: @A+ @A 1+ !A ;

: highNibble ( byteAddr -- highNibbleAddr )
    2 * ;
    
: lowNibble ( byteAddr -- lowNibbleAddr )
    2 * 1 + ;
    
: nibble@ ( nibbleAddr -- val )
    2 /mod c@ swap [ 4 >> ] ifFalse $F and ;
    
: visible?
    dup 32 > swap 126 < + -2 = ;
    
: printField
    dup 10 < [ 48 + ] [ 55 + ] if putc ;
    
: printWords
    highWord highByte highNibble
    8 [  dup nibble@ printField 1+ ] 
    times drop space ;
    
: printChar
    dup visible? [ putc ] [ drop 95 putc ] if ;
    
: printChars
    highWord highByte
    16 [ dup c@ printChar 1+ ]
    times drop cr ;
    
: dumpLine ( addr -- )
   @A putn space
   4 [ @A printWords @A+ ] times 
   @B printChars @A !B ;
   
---reveal---

: dump ( numLines addr -- )
   cr dup !A !B  [ dumpLine ] times ;
   
}}
;chain

with dump'
   
variable myVar
" hello world " myVar !

: testDump
    5 myVar @ dump
    myVar @ packString
    5 myVar @ dump ;
