needs subcell'

chain: dump'
{{
  variables| A B |

  : highNibble ( byteAddr -- highNibbleAddr )
    2 * ;

  : lowNibble ( byteAddr -- lowNibbleAddr )
    2 * 1 + ;

  : nibble@ ( nibbleAddr -- val )
    2 /mod ^subcell'c@ swap [ 4 >> ] ifFalse $F and ;

  : visible? ( val -- t | f )
    31 126 within ;

  : printField ( val -- )
    dup 10 < [ 48 + ] [ 55 + ] if putc ;

  : printWords ( addr -- )
    ^subcell'highWord ^subcell'highByte highNibble
    8 [ dup nibble@ printField 1+ ] times drop space ;

  : printChar ( val -- )
    dup visible? [ putc ] [ drop 95 putc ] if ;

  : printChars ( addr -- )
    ^subcell'highWord ^subcell'highByte
    16 [ dup ^subcell'c@ printChar 1+ ] times drop cr ;

  : dumpLine ( addr -- )
    @A putn space
    4 [ @A printWords A ++ ] times @B printChars @A !B ;
---reveal---
  : dump ( numLines addr -- )
    cr dup !A !B [ dumpLine ] times ;
}}

;chain

