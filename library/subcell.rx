
: highWord ( cellAddr -- highwordAddr ) 
   2 * ;

: lowWord  ( cellAddr -- lowwordAddr  ) 
   2 * 1 + ;

: w@ ( wordAddr -- val ) 
   2 /mod  @ swap [ 16 >> ] ifFalse $FFFF and ;
    
: w@+ ( wordAddr -- wordAddr val ) 
   dup 1+ push w@ pop swap ;

: w! ( val wordAddr -- )
   2 /mod dup push swap 
   [ @ $FFFF0000 and + ] 
   [ @ $0000FFFF and swap 16 << + ] 
   if pop ! ;
   
: w!+ ( val wordAddr -- wordAddr+1 )
   dup 1+ push w! pop ;

: highByte ( wordAddr -- highbyteAddr ) 
   2 * ;

: lowByte  ( wordAddr -- lowbyteAddr  ) 
   2 * 1 + ;

: c@ ( byteAddr -- val )
   2 /mod w@ swap [ 8 >> ] ifFalse $FF and ;
   
: c@+ ( byteAddr -- byteAddr val )
  dup 1+ push c@ pop swap ;

: c! ( val byteaddr -- ) 
  2 /mod dup push swap 
  [ w@ $FF00 and + ] 
  [ w@ $00FF and swap 8 << + ] 
  if pop w! ;
  
: c!+ ( val byteAddr -- byteAddr+1 )
   dup 1+ push c! pop ;
    
: packString ( strAddr -- )
    dup highWord highByte
    [ repeat push @+ pop swap 0; swap c!+ again ] 
    do 0 c! drop ;
    
: printPack ( strAddr -- )
    highWord highByte
    [ repeat c@+ 0; putc again ] 
    do drop ;
    
: getPackedLength ( strAddr -- strLen )
    highWord highByte 
    [ 0 swap repeat c@+ 0; drop swap 1+ swap again ]
    do drop ;
   

variable testVal

: testWord
   $FFFF0001 testVal !
   cr " High: " puts testVal highWord w@ putn
   cr " Low: "  puts testVal lowWord  w@ putn 
   $0000 testVal highWord w! 
   $FFFF testVal lowWord w! 
   cr " High: " puts testVal highWord w@ putn
   cr " Low: "  puts testVal lowWord  w@ putn ; 
   
: testWords
    1 2 swap testVal highWord w!+ w!+ drop
    testVal highWord 
    cr w@+ putn 
    cr w@+ putn cr ;

: testByte
    $FF01 testVal !
    cr " High: " puts testVal lowWord highByte c@ putn
    cr " Low: "  puts testVal lowWord lowByte  c@ putn
    $00 testVal lowWord highByte c! 
    $FF testVal lowWord lowByte  c! 
    cr " High: " puts testVal lowWord highByte c@ putn
    cr " Low: "  puts testVal lowWord lowByte  c@ putn ; 
    
: testBytes
    4 3 2 1 testVal highWord highByte c!+ c!+ c!+ c!+ drop
    testVal highWord highByte 
    cr c@+ putn 
    cr c@+ putn 
    cr c@+ putn 
    cr c@+ putn cr ;

